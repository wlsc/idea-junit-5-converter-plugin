import org.jsoup.Jsoup
import org.jsoup.nodes.Document

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.21'
    id 'org.asciidoctor.jvm.convert' version '3.2.0'
}

group 'de.wlsc'
version '1.3.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.javaparser:javaparser-core:3.16.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:1.5.22.RELEASE'
}

wrapper {
    gradleVersion = '6.5.1'
    distributionType = Wrapper.DistributionType.ALL
}

asciidoctor {
    sourceDir file('.')
    outputDir file('build/docs')
    sources {
        include 'README.adoc'
    }
}

intellij {
    plugins = ['java']
    version = '2020.1.4'
}

patchPluginXml {
    dependsOn asciidoctor
    sinceBuild '181'
    untilBuild = ''
    changeNotes = {
        getReadme().select(".sect2").html()
    }
    pluginDescription = {
        getReadme().select(".sect1").get(0).select(".sectionbody").html()
    }
}

Document getReadme() {
    Jsoup.parse(file('build/docs/README.html').getText('UTF-8'))
}
