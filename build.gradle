import org.jsoup.Jsoup
import org.jsoup.nodes.Document

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
    id 'org.asciidoctor.jvm.convert' version '3.3.0'
}

group 'de.wlsc'
version '1.3.2'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.javaparser:javaparser-core:3.18.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:1.5.22.RELEASE'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testRuntime 'org.junit.vintage:junit-vintage-engine:5.7.0'
}

wrapper {
    gradleVersion = '6.7.1'
    distributionType = Wrapper.DistributionType.ALL
}

asciidoctor {
    sourceDir file('.')
    outputDir file('build/docs')
    sources {
        include 'README.adoc'
    }
}

intellij {
    plugins = ['java']
    version = '2020.3'
}

patchPluginXml {
    dependsOn asciidoctor
    sinceBuild '193'
    untilBuild = ''
    changeNotes = {
        getReadme().select(".sect2").html()
    }
    pluginDescription = {
        getReadme().select(".sect1").get(0).select(".sectionbody").html()
    }
}

Document getReadme() {
    Jsoup.parse(file('build/docs/README.html').getText('UTF-8'))
}
